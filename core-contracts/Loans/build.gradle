/*
 * Copyright (c) 2022-2022 Balanced.network.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import network.balanced.score.dependencies.Dependencies

version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly Dependencies.javaeeApi
    implementation project(':score-lib')

    implementation Dependencies.javaeeScorex
    implementation Dependencies.minimalJson

    testImplementation Dependencies.javaeeUnitTest
    testImplementation Dependencies.junitJupiter
    testRuntimeOnly Dependencies.junitJupiterEngine
    testImplementation Dependencies.mockitoCore
    testImplementation Dependencies.mockitoInline
    testImplementation Dependencies.javaeeTokens
    testImplementation project(':test-lib')

    intTestImplementation project(':DexMock')
    intTestImplementation project(':testinteg')
    intTestImplementation project(':RebalanceMock')
    intTestImplementation Dependencies.iconSdk
}

optimizedJar {
    mainClassName = 'network.balanced.score.core.loans.LoansImpl'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}
    }

    enableDebug = false
}

deployJar {
    endpoints {
        sejong {
            uri = 'https://sejong.net.solidwallet.io/api/v3'
            nid = 0x53
        }
        local {
            uri = 'http://localhost:9082/api/v3'
            nid = 0x3
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
    parameters {
        arg('_governance', 'hxe5679f118e093657b71967b0dfddb4d00cbd80b4')
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport 
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

def dexJar = project(':DexMock').getTasks().getByName('optimizedJar')
def rebalanceJar = project(':RebalanceMock').getTasks().getByName('optimizedJar')

task integrationTest(type: Test, dependsOn: optimizedJar) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    testLogging.showStandardStreams = true

    // use the common config files
    systemProperty('env.props', new File(project(':testinteg').projectDir, 'conf/env.props'))

    def prefix = 'score.path.'
    systemProperty(prefix + project.name, optimizedJar.outputJarName)
    systemProperty(prefix + 'RebalanceMock', rebalanceJar.outputJarName)
    systemProperty(prefix + 'DexMock', dexJar.outputJarName)
}
integrationTest.dependsOn(rebalanceJar)
integrationTest.dependsOn(dexJar)
